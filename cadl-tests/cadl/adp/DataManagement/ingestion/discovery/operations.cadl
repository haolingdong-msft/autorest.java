using Cadl.Http;
using Cadl.Rest;
using Azure.Core;
using Azure.Core.Foundations.Extensions;
using OpenAPI;
using Autorest;

namespace Adp.DataManagement;

@client(DiscoveryClients.DiscoveryClient)
@tag("Discovery")
interface DiscoveryOperations {
  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom operation."
  @doc("Creates a new ingestion discovery instance.")
  createOrReplace is CustomResourceCreateOrReplace<
    Discovery,
    CustomBodyWrapper<DiscoveryCreationParameters>
  >;

  @doc("Get discovery by ID.")
  get is ResourceRead<Discovery>;

  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom LRO response template the LRO actions."
  @doc("Initiates the process of completing the discovery and creating the upload file grouping manifest files.")
  @extension("x-ms-long-running-operation", true)
  @asyncOperationOptions("location")
  @pollingOperation(LongRunningOperations.getStatus)
  @finalOperation(DiscoveryOperations.get)
  complete is CustomLongRunningResourceAction<
    Discovery,
    {},
    {},
    DiscoveryLroResponse
  >;


  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom LRO response template the LRO actions."
  @doc("Initiates the process of cancelling the discovery.")
  @extension("x-ms-long-running-operation", true)
  @asyncOperationOptions("location")
  @pollingOperation(LongRunningOperations.getStatus)
  cancel is CustomLongRunningResourceAction<
    Discovery,
    {},
    {},
    DiscoveryLroResponse
  >;
}

@client(DiscoveryClients.DiscoverySpecialFilesClient)
@tag("Discovery")
interface DiscoverySpecialFileOperations {
  /*   
  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom resource list operation."
  @doc("""
  List special files details for the discovery resource.
  Returns SAS signed URI that allows reading special files from Azure Storage.
  This URI expires in 24 hours.
  """)
  list is CustomResourceList<DiscoverySpecialFile>; 
  */

  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom LRO response template the LRO actions."
  @doc("Initiates the process of generating SAS signed URIs for uploading special files for the discovery.")
  @extension("x-ms-long-running-operation", true)
  @asyncOperationOptions("location")
  @pollingOperation(LongRunningOperations.getStatus)
  generate is CustomLongRunningResourceCollectionAction<
    DiscoverySpecialFile,
    {},
    DiscoverySpecialFile,
    DiscoveryLroResponse
  >;

  @doc("""
  List special files details for the discovery resource.
  Returns SAS signed URI that allows uploading special files to Azure Storage.
  This URI expires in 24 hours.
  """)
  listWritableUris is ResourceCollectionAction<
    DiscoverySpecialFile,
    {},
    Page<DiscoverySpecialFile>
  >;
}

@client(DiscoveryClients.DiscoveryUploadsClient)
@tag("Discovery")
interface DiscoveryResultUploadOperations {
  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom resource list operation."
  @doc("List upload detail for the discovery resource.")
  list is CustomResourceList<DiscoveryUpload>;
}
